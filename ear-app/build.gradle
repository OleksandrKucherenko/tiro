apply plugin: 'ear'
apply plugin: 'java'

ear {
  /* CUSTOM ENTRIES for application.xml */
  deploymentDescriptor {
    webModule('war-time.war', project(':war-time').gretty.contextPath)
    webModule('war-tasks.war', project(':war-tasks').gretty.contextPath)
  }

  /* Remove GIT empty folder .placeholder files from final binary. */
  exclude('**/.placeholder')

}

dependencies {
  deploy project(path: ':war-api', configuration: 'archives')
  deploy project(path: ':war-tasks', configuration: 'archives')
  deploy project(path: ':war-time', configuration: 'archives')

  earlib 'org.slf4j:slf4j-api:+'
}

/* [ UNIT TESTING ] ================================================================================================= */

tasks.compileTestJava.onlyIf { useTdd }
test.onlyIf { useTdd }

if (useJacoco) {
  apply plugin: 'jacoco'

  jacoco {
    toolVersion = '+'
  }
}

test {
  /* Print tests results to the output */
  testLogging {
    // set options for log level LIFECYCLE
    events "failed", "passed"

    // set options for log level DEBUG
    debug.events "failed", "passed", "skipped", "standardError", "standardOut", "started"

    // set options for log level INFO
    info.events = ["failed", "passed", "skipped"]
  }

  /* Disable separated test reports, we use aggregated */
  reports {
    html.enabled = false
  }
}